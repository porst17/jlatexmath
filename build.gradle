/******************************************
 * buildscript configuration
 ******************************************/

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

/******************************************
 * project configuration
 ******************************************/

group = 'org.scilab.forge'
version = '1.0.3'

targetCompatibility = 1.5
sourceCompatibility = 1.5

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

sourceSets {
    main {
        java {
            srcDir 'src'
            include '**/*.java'
        }
        resources {
            srcDir 'src'
            exclude '**/*.java'
        }
    }
}

jar {
    manifest {
        from file( "${projectDir}/MANIFEST.MF" )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    // the bintray.com user name and API key have to be specified as bintray.user and bintray.key in gradle.properties
    ['bintray.user', 'bintray.key'].each { prop ->
        assert project.properties.containsKey( prop ), "supply $prop in the file gradle.properties"
    }
    user = project.properties[ 'bintray.user' ]
    key = project.properties[ 'bintray.key' ]

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'org.scilab.forge:jlatexmath'
        desc = 'JLaTeXMath - A Java API to render LaTeX'
        // userOrg = user
        licenses = ['GPL-2.0']
        websiteUrl = 'http://forge.scilab.org/index.php/p/jlatexmath/'
        issueTrackerUrl = 'http://forge.scilab.org/index.php/p/jlatexmath/issues/'
        vcsUrl = 'http://forge.scilab.org/index.php/p/jlatexmath/source/tree/master/'
        // labels = ['gear', 'gore', 'gorilla']
        publicDownloadNumbers = true
        // attributes= ['a': ['ay1', 'ay2'], 'b': ['bee'], c: 'cee'] //Optional package-level attributes
        version {
            name = '1.0.3'
            desc = 'JLaTeXMath v1.0.3'
            vcsTag = '1.0.3'
            // attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
